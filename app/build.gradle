apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//加载资源.在本地的 local.properties 文件设置了一些参数，从这里获取
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 28

    // 签名文件
    signingConfigs {
        config {
            keyAlias 'lyl'
            keyPassword '123456'
            storeFile file('../lyl.jks')
            storePassword '123456'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    defaultConfig {
        applicationId "com.lyl.boon"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 5
        versionName "1.5.0"
        multiDexEnabled true
        flavorDimensions "App"
        ndk {
            abiFilters 'armeabi' //设置支持的SO库架构, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    // 多渠道/多环境 的不同配置
    productFlavors {
        dev {
            // 每个环境包名不同
            applicationId "com.lyl.boon.dev"
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "dev_boon"
            resValue "bool", "isrRank", 'false'
            // 动态修改 常量 字段
            buildConfigField "String", "ENVIRONMENT", '"dev"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')
            buildConfigField "String", "LEANCLOUD_APPID", properties.getProperty('leancloudAppId')
            buildConfigField "String", "LEANCLOUD_APPKEY", properties.getProperty('leancloudAppKey')
            // 修改 AndroidManifest.xml 里渠道变量
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "dev",
                                    app_icon           : "@drawable/ic_launcher"]

            dimension "App"
        }
        stage {
            applicationId "com.lyl.boon.stage"

            resValue "string", "app_name", "stage_boon"
            resValue "bool", "isrRank", 'true'

            buildConfigField "String", "ENVIRONMENT", '"stage"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')
            buildConfigField "String", "LEANCLOUD_APPID", properties.getProperty('leancloudAppId')
            buildConfigField "String", "LEANCLOUD_APPKEY", properties.getProperty('leancloudAppKey')

            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "stage",
                                    app_icon           : "@drawable/logo"]

            dimension "App"
        }
        prod {
            applicationId "com.lyl.boon"

            resValue "string", "app_name", "boon"
            resValue "bool", "isrRank", 'true'

            buildConfigField "String", "ENVIRONMENT", '"prod"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')
            buildConfigField "String", "LEANCLOUD_APPID", properties.getProperty('leancloudAppId')
            buildConfigField "String", "LEANCLOUD_APPKEY", properties.getProperty('leancloudAppKey')

            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "prod",
                                    app_icon           : "@drawable/logo"]

            dimension "App"
        }
    }

    //移除lint检测的error
    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }

        release {
            minifyEnabled false
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            // 批量打包
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.okhttp3:okhttp:4.1.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    implementation 'com.commit451:PhotoView:1.2.5'
    implementation 'com.jaeger.statusbarutil:library:1.5.1'
    implementation 'com.roughike:bottom-bar:2.3.1'
    //Material Dialog
    implementation 'me.drakeet.materialdialog:library:1.3.1'

    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.2.0'

    implementation 'io.reactivex:rxjava:1.2.3'
    implementation 'io.reactivex:rxandroid:1.2.1'

    implementation 'com.github.githubwing:ByeBurger:1.2.3'
    //一个不错的BaseAapter封装类
    implementation 'org.byteam.superadapter:superadapter:3.4'

    //非常不错的 loading 加载库
    implementation 'com.wang.avi:library:1.0.5'
    implementation 'com.nineoldandroids:library:2.4.0'
    //应用升级
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.2'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    implementation 'cn.leancloud:storage-android:6.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}
